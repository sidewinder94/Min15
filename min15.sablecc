Helpers

  upper = ['A'..'Z'];
  lower = ['a'..'z'];
  digit = ['0'..'9'];

  name_char = [[upper + lower] + digit];

  string_char = [[32..126]-['\'+'"']] | '\"' | '\\';

  cr = 13;
  lf = 10;
  tab = 9;

  not_eol = [[0..0xffff]-[cr+lf]];

Tokens

  class = 'class';
  super = 'super';
  var = 'var';
  fun = 'fun';
  intern = 'intern';
  end = 'end';
  do = 'do';
  while = 'while';
  if = 'if';
  then = 'then';
  else = 'else';
  return = 'return';
  is = 'is';
  isa = 'isa';
  as = 'as';
  new = 'new';
  null = 'null';
  false = 'false';
  true = 'true';
  self = 'self';
  and = 'and';
  or = 'or';
  not = 'not';

  l_par = '(';
  r_par = ')';
  plus = '+';
  minus = '-';
  star = '*';
  slash = '/';
  percent = '%';
  eq = '=';
  neq = '!=';
  lt = '<';
  gt = '>';
  lteq = '<=';
  gteq = '>=';

  comma = ',';
  colon = ':';
  dot = '.';

  id = lower name_char*;
  class_name = upper name_char*;
  field_name = '_' lower name_char*;
  invalid_field_name = '_' name_char*;

  number = digit+;
  invalid_number = digit name_char*;

  string = '"' string_char* '"';
  invalid_string = '"' string_char*;

  eol = cr | lf | cr lf;

  blank = (' ' | tab)+;
  comment = '#' not_eol*;

Ignored Tokens
    blank, comment;


Productions

  file =
    [class_defs]:class_def* stmts [eols]:eol*;

  class_def =
    [eols1]:eol* [class_keyword]:class class_name super_decl? [eol1]:eol [members]:member* [eols2]:eol* end [eol2]:eol;

  super_decl =
    eol super class_name;

  member =
    {field} [eols]:eol* var field_name eol |
    {method} [eols]:eol* fun id l_par params? r_par return_decl? [eol1]:eol? do [eol2]:eol stmts end [eol3]:eol |
    {operator} [eols]:eol* fun operator l_par params? r_par return_decl? [eol1]:eol? do [eol2]:eol stmts end [eol3]:eol |
    {intern_method} [eols]:eol* intern fun id l_par params? r_par return_decl? eol |
    {intern_operator} [eols]:eol* intern fun operator l_par params? r_par return_decl? eol;

  params =
    [eol1]:eol? param [additional_params]:additional_param* [eol2]:eol?;

  additional_param =
    comma eol? param;

  param =
    id colon class_name;

  return_decl =
    colon class_name;

  operator =
    {eq} eq |
    {neq} neq |
    {lt} lt |
    {gt} gt |
    {lteq} lteq |
    {gteq} gteq |
    {plus} plus |
    {minus} minus |
    {star} star |
    {slash} slash |
    {percent} percent;

  stmts =
    [stmts]:stmt*;

  stmt =
    {var_def} [eols]:eol* var id colon class_name eol |
    {var_init} [eols]:eol* var id eq [eol1]:eol? exp [eol2]:eol |
    {var_assign} [eols]:eol* id eq [eol1]:eol? exp [eol2]:eol |
    {field_assign} [eols]:eol* field_name eq [eol1]:eol? exp [eol2]:eol |
    {call} [eols]:eol* call eol |
    {self_call} [eols]:eol* self_call eol |
    {while} [eols]:eol* while [eol1]:eol? exp [eol2]:eol? do [eol3]:eol stmts end [eol4]:eol |
    {if} [eols]:eol* if [eol1]:eol? exp [eol2]:eol? then [eol3]:eol stmts else_part? end [eol4]:eol |
    {return} [eols]:eol* return exp? eol;

  else_part =
    else eol stmts;

  exp =
    {or} exp or eol? conjunction |
    {simple} conjunction;

  conjunction =
    {and} conjunction and eol? comparison |
    {simple} comparison;

  comparison =
    {eq} comparison eq eol? arith_exp |
    {neq} comparison neq eol? arith_exp |
    {lt} comparison lt eol? arith_exp |
    {gt} comparison gt eol? arith_exp |
    {lteq} comparison lteq eol? arith_exp |
    {gteq} comparison gteq eol? arith_exp |
    {is} comparison is eol? arith_exp |
    {simple} arith_exp;

  arith_exp =
    {add} arith_exp plus eol? factor |
    {sub} arith_exp minus eol? factor |
    {simple} factor;

  factor =
    {mul} factor star eol? left_unary_exp |
    {div} factor slash eol? left_unary_exp |
    {mod} factor percent eol? left_unary_exp |
    {simple} left_unary_exp;

  left_unary_exp =
    {not} not left_unary_exp |
    {neg} minus left_unary_exp |
    {simple} right_unary_exp;

  right_unary_exp =
    {call} call |
    {isa} right_unary_exp isa class_name |
    {as} right_unary_exp as class_name |
    {simple} term;

  term =
    {self_call} self_call |
    {par} l_par [eol1]:eol? exp [eol2]:eol? r_par |
    {new} new class_name |
    {field} field_name |
    {var} id |
    {num} number |
    {null} null |
    {self} self |
    {true} true |
    {false} false |
    {string} string;

  call =
    right_unary_exp dot eol? id l_par args? r_par;

  self_call =
    id l_par args? r_par;

  args =
    [eol1]:eol? arg [additional_args]:additional_arg* [eol2]:eol?;

  additional_arg =
    comma eol? arg;

  arg =
    exp;
